#define BLYNK_TEMPLATE_ID "TMPL6k2hKAOUX"
#define BLYNK_TEMPLATE_NAME "Air Quality Monitor"
#define BLYNK_AUTH_TOKEN "zBEZC5F7mKjnyTmB-dkDqZZrt2HT1Sog"

#include <ESP8266WiFi.h>
#include <ESP8266HTTPClient.h>
#include <BlynkSimpleEsp8266.h>

char ssid[] = "iicha"; 
char pass[] = "iicha84b1"; 
char auth[] = BLYNK_AUTH_TOKEN;

// Web server URL
const char* SERVER_URL = "http://192.168.1.55:5000/api/predict";

WiFiClient client;
HTTPClient http;

unsigned long lastPredictTime = 0;
const unsigned long PREDICT_INTERVAL = 300;  // Goi API moi 300ms (real-time)
unsigned long lastBlynkTime = 0;
const unsigned long BLYNK_INTERVAL = 500;  // Gui Blynk moi 500ms

void setup() {
  Serial.begin(9600);
  
  WiFi.begin(ssid, pass);
  while (WiFi.status() != WL_CONNECTED) {
    delay(500);
  }
  
  Blynk.begin(auth, ssid, pass);
}

void sendAlertToArduino(int alertLevel) {
  // Gui alert level ve Arduino qua Serial (hardware Serial cua ESP8266)
  // Format: "ALERT:2\n"
  Serial.print("ALERT:");
  Serial.println(alertLevel);
  delay(10);  // Cho Serial gui xong
}

int parseAlertLevel(String jsonResponse) {
  int alertIndex = jsonResponse.indexOf("\"alert_level\":");
  if (alertIndex < 0) {
    return -1;
  }
  
  int startIndex = alertIndex + 14;
  int endIndex = startIndex;
  
  while (endIndex < jsonResponse.length()) {
    char c = jsonResponse.charAt(endIndex);
    if (c == ',' || c == '}' || c == ' ' || c == '\n' || c == '\r') {
      break;
    }
    endIndex++;
  }
  
  String levelStr = jsonResponse.substring(startIndex, endIndex);
  levelStr.trim();
  
  int level = levelStr.toInt();
  if (level < 0 || level > 3) {
    return -1;
  }
  
  return level;
}

int getAlertFromAI(float mq135, float mq7, float pm25, float sound) {
  if (WiFi.status() != WL_CONNECTED) {
    return -1;
  }
  
  // FALLBACK: Kiểm tra giá trị cảm biến trước khi gọi API
  if (mq7 > 35 || pm25 > 150 || mq135 > 200 || sound > 85) {
    return 3;  // NGUY HIEM
  }
  if (mq7 > 9 || pm25 > 75 || mq135 > 100 || sound > 65) {
    return 2;  // CANH BAO
  }
  
  http.begin(client, SERVER_URL);
  http.addHeader("Content-Type", "application/json");
  http.setTimeout(1000);
  
  String jsonData = "{";
  jsonData += "\"mq135\":" + String(mq135) + ",";
  jsonData += "\"mq7\":" + String(mq7) + ",";
  jsonData += "\"pm25\":" + String(pm25) + ",";
  jsonData += "\"sound\":" + String(sound);
  jsonData += "}";
  
  int httpCode = http.POST(jsonData);
  
  if (httpCode == 200) {
    String response = http.getString();
    int alertLevel = parseAlertLevel(response);
    http.end();
    
    if (alertLevel >= 0 && alertLevel <= 3) {
      return alertLevel;
    }
  }
  
  http.end();
  
  // FALLBACK: Nếu API fail, dùng logic local
  if (mq7 > 35 || pm25 > 150 || mq135 > 200 || sound > 85) {
    return 3;
  }
  if (mq7 > 9 || pm25 > 75 || mq135 > 100 || sound > 65) {
    return 2;
  }
  
  return 1;  // AN TOAN (default)
}

void loop() {
  Blynk.run();
  
  if (Serial.available() > 0) {
    String data = Serial.readStringUntil('\n');
    data.trim();
    
    int firstComma  = data.indexOf(',');
    int secondComma = data.indexOf(',', firstComma + 1);
    int thirdComma  = data.indexOf(',', secondComma + 1);
    
    if (firstComma > 0 && secondComma > firstComma && thirdComma > secondComma) {
      float mq135_val = data.substring(0, firstComma).toFloat();
      float mq7_val   = data.substring(firstComma + 1, secondComma).toFloat();
      float dust_val  = data.substring(secondComma + 1, thirdComma).toFloat();
      float noise_val = data.substring(thirdComma + 1).toFloat();
      
      // Gui len Blynk (moi 500ms)
      if (millis() - lastBlynkTime >= BLYNK_INTERVAL) {
        Blynk.virtualWrite(V0, mq135_val);
        Blynk.virtualWrite(V1, mq7_val);
        Blynk.virtualWrite(V2, dust_val);
        Blynk.virtualWrite(V3, noise_val);
        lastBlynkTime = millis();
      }
      
      // Goi AI server moi 300ms (real-time)
      if (millis() - lastPredictTime >= PREDICT_INTERVAL) {
        int alertLevel = getAlertFromAI(mq135_val, mq7_val, dust_val, noise_val);
        
        if (alertLevel >= 0) {
          // Gui alert level ve Arduino
          sendAlertToArduino(alertLevel);
        }
        
        lastPredictTime = millis();
      }
    }
  }
}
