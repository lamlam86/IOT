#include <MQUnifiedsensor.h>
#include <SoftwareSerial.h>
#include <LiquidCrystal.h>

// LCD 16x2: RS=8, E=9, D4=10, D5=11, D6=2, D7=3
LiquidCrystal lcd(8, 9, 10, 11, 2, 3);

// SoftwareSerial gửi sang ESP8266
// Arduino TX(12) -> ESP8266 RX
// Arduino RX(13) <- ESP8266 TX
SoftwareSerial espSerial(13, 12);

// Cấu hình MQ
#define Board                 ("Arduino UNO")
#define Voltage_Resolution    (5)
#define ADC_Bit_Resolution    (10)

// MQ135
#define Pin135                (A0)
#define Type135               ("MQ-135")
MQUnifiedsensor MQ135(Board, Voltage_Resolution, ADC_Bit_Resolution, Pin135, Type135);

// MQ7
#define Pin7                  (A1)
#define Type7                 ("MQ-7")
MQUnifiedsensor MQ7(Board, Voltage_Resolution, ADC_Bit_Resolution, Pin7, Type7);

// Dust Sensor (tự viết)
#define DUST_PIN              A2
#define LED_PIN               7

// Sound Sensor
#define SOUND_PIN             A3

// Bien luu alert level tu AI
int aiAlertLevel = 0;
unsigned long lastAlertTime = 0;
const unsigned long ALERT_TIMEOUT = 3000;  // 3 giay

// Hàm hiển thị alert text
String getAlertText(int level) {
  switch(level) {
    case 0: return "TRONG LANH";
    case 1: return "AN TOAN";
    case 2: return "CANH BAO";
    case 3: return "NGUY HIEM";
    default: return "UNKNOWN";
  }
}

// Hàm hiển thị LCD
void displayLCD(float mq135, float mq7, float pm25, float sound, int alert) {
  lcd.clear();
  
  // Dòng 1: Hiển thị cảm biến chính
  lcd.setCursor(0, 0);
  lcd.print("MQ:");
  if (mq135 < 100) {
    lcd.print(mq135, 1);
  } else {
    lcd.print(mq135, 0);
  }
  lcd.print(" PM:");
  lcd.print(pm25, 0);
  
  // Dòng 2: CO, Sound và Alert (rút gọn)
  lcd.setCursor(0, 1);
  lcd.print("CO:");
  lcd.print(mq7, 2);
  lcd.print(" S:");
  lcd.print(sound, 0);
  lcd.print(" ");
  
  // Hiển thị alert level (rút gọn để vừa màn hình 16 ký tự)
  switch(alert) {
    case 0: lcd.print("TRONG"); break;
    case 1: lcd.print("ANTO"); break;
    case 2: lcd.print("CANH"); break;
    case 3: lcd.print("NGUY"); break;
    default: lcd.print("?"); break;
  }
}

float getDustDensity() {
  digitalWrite(LED_PIN, LOW);
  delayMicroseconds(280);
  int val = analogRead(DUST_PIN);
  delayMicroseconds(40);
  digitalWrite(LED_PIN, HIGH);
  delayMicroseconds(9680);
  
  float voltage = val * (5.0 / 1024.0);
  float dust = (voltage - 0.9) * 200.0;
  return (dust < 0) ? 0 : dust;
}

float getDecibel() {
  unsigned long start = millis();
  int sMax = 0, sMin = 1024;
  while (millis() - start < 30) {
    int s = analogRead(SOUND_PIN);
    if (s > sMax) sMax = s;
    if (s < sMin) sMin = s;
  }
  return map(sMax - sMin, 0, 700, 35, 95);
}

int calculateFallbackAlert(float mq135, float mq7, float pm25, float sound) {
  // Logic fallback giống web server
  if (mq7 > 35 || pm25 > 150 || mq135 > 200 || sound > 85) {
    return 3;  // NGUY HIEM
  }
  if (mq7 > 9 || pm25 > 75 || mq135 > 100 || sound > 65) {
    return 2;  // CANH BAO
  }
  return 1;  // AN TOAN
}

void checkESP8266Response() {
  while (espSerial.available() > 0) {
    String response = espSerial.readStringUntil('\n');
    response.trim();
    
    if (response.startsWith("ALERT:")) {
      int colonIndex = response.indexOf(':');
      if (colonIndex > 0) {
        String levelStr = response.substring(colonIndex + 1);
        levelStr.trim();
        int level = levelStr.toInt();
        if (level >= 0 && level <= 3) {
          aiAlertLevel = level;
          lastAlertTime = millis();
        }
      }
    }
  }
}

void setup() {
  Serial.begin(9600);
  espSerial.begin(9600);
  pinMode(LED_PIN, OUTPUT);
  digitalWrite(LED_PIN, HIGH);
  
  // Khởi tạo LCD
  lcd.begin(16, 2);
  lcd.setCursor(0, 0);
  lcd.print("IoT Monitor");
  lcd.setCursor(0, 1);
  lcd.print("Initializing...");
  delay(500);
  
  Serial.println(F("=== IoT Air & Sound Monitor ==="));
  
  // MQ135
  MQ135.setRegressionMethod(1);
  MQ135.setA(110.47);
  MQ135.setB(-2.862);
  MQ135.init();
  
  Serial.println(F("Calibrating MQ135..."));
  lcd.setCursor(0, 0);
  lcd.print("Cal MQ135...    ");
  float calcR0_135 = 0;
  for (int i = 0; i < 10; i++) {
    MQ135.update();
    calcR0_135 += MQ135.calibrate(3.6);
    delay(100);
  }
  MQ135.setR0(calcR0_135 / 10);
  Serial.print(F("MQ135 R0 = ")); Serial.println(MQ135.getR0());
  
  // MQ7
  MQ7.setRegressionMethod(1);
  MQ7.setA(99.042);
  MQ7.setB(-1.518);
  MQ7.init();
  
  Serial.println(F("Calibrating MQ7..."));
  lcd.setCursor(0, 0);
  lcd.print("Cal MQ7...      ");
  float calcR0_7 = 0;
  for (int i = 0; i < 10; i++) {
    MQ7.update();
    calcR0_7 += MQ7.calibrate(27.5);
    delay(100);
  }
  MQ7.setR0(calcR0_7 / 10);
  Serial.print(F("MQ7 R0 = ")); Serial.println(MQ7.getR0());
  
  // Hiển thị "Ready" trên LCD
  lcd.clear();
  lcd.setCursor(0, 0);
  lcd.print("System Ready!");
  lcd.setCursor(0, 1);
  lcd.print("Waiting data...");
  delay(1000);
  
  Serial.println(F("San sang!"));
}

void loop() {
  // Doc phan hoi tu ESP8266 (doc nhieu lan de khong bo sot)
  checkESP8266Response();
  
  MQ135.update();
  float valueMQ135 = MQ135.readSensor();
  
  MQ7.update();
  float valueMQ7 = MQ7.readSensor();
  
  float valueDust = getDustDensity();
  float valueNoise = getDecibel();
  
  // Fallback: Nếu không nhận được alert từ ESP8266 trong 3 giây, tính local
  int finalAlert = aiAlertLevel;
  if (millis() - lastAlertTime > ALERT_TIMEOUT) {
    finalAlert = calculateFallbackAlert(valueMQ135, valueMQ7, valueDust, valueNoise);
  } else {
    // Đồng bộ: Kiểm tra lại với giá trị hiện tại
    int fallbackAlert = calculateFallbackAlert(valueMQ135, valueMQ7, valueDust, valueNoise);
    if (fallbackAlert > aiAlertLevel) {
      finalAlert = fallbackAlert;  // Ưu tiên mức cao hơn
    }
  }
  
  // Hiển thị lên LCD (mỗi 200ms để real-time hơn)
  static unsigned long lastLCDUpdate = 0;
  if (millis() - lastLCDUpdate >= 200) {
    displayLCD(valueMQ135, valueMQ7, valueDust, valueNoise, finalAlert);
    lastLCDUpdate = millis();
  }
  
  Serial.print(valueMQ135); Serial.print(",");
  Serial.print(valueMQ7);   Serial.print(",");
  Serial.print(valueDust);  Serial.print(",");
  Serial.print(valueNoise);
  Serial.print(" | AI Alert: "); Serial.print(finalAlert);
  Serial.print(" ("); Serial.print(getAlertText(finalAlert)); Serial.println(")");
  
  espSerial.print(valueMQ135); espSerial.print(",");
  espSerial.print(valueMQ7);   espSerial.print(",");
  espSerial.print(valueDust);  espSerial.print(",");
  espSerial.println(valueNoise);
  
  delay(10);  // Real-time: 10ms = 100Hz
}
